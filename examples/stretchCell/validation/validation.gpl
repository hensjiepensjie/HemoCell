set term pngcairo size 1024,768
set output "validation.png"

set rmargin 20
set multiplot layout 2,1 rowsfirst

set xlabel "Iteration"
set ylabel "Displacement (micron)"

forces="0 25 50 75 125 175"

# Draw axial and transverse displacements versus hemocell iterations
plot for [f=1:words(forces)] "stretch-".word(forces, f).".log" using 1:2 \
     with lines title word(forces,f).'pN' at end linecolor rgb "orange", \
     for [f=1:words(forces)] "stretch-".word(forces, f).".log" using 1:3 \
     with lines notitle linecolor rgb "blue"

set xlabel "Stretch force (pN)"
set key outside
set key right top

# Draw the final axial and transverse displacements compared to the reference
# published values and experimental bounds.
plot 'combined.txt' using 1:3 with linespoints title "A" linecolor rgb "orange", \
     'combined.txt' using 1:4 with linespoints title "T" linecolor rgb "blue", \
     'reference-axial.dat' using 1:2 with linespoints title "RA" linecolor rgb "black", \
     'reference-transverse.dat' using 1:2 with linespoints title "RT" linecolor rgb "black", \
     'reference-bounds.dat' using 1:2 with points pt 2 linecolor rgb "gray" title "exp"
